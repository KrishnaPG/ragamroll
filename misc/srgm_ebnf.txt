// antlr grammar created using antlrworks

grammar srgm;

composition :	(WS)? (directive* time_consumer)+ ;
		
time_consumer : (note|rest);

directive : (short_octave_directive | octave_directive | length_directive | raga_directive | tala_directive | jfugue_passthru | phrase_separator);

octave_directive : ('O' '=') NUMBER (WS+|EOF);

length_directive : ('L' '=') NUMBER (WS+|EOF); 

short_octave_directive 
	: OCTAVE_SHIFTER (WS+|EOF);

phrase_separator 
	: PHRASE_SEPARATOR (WS+|EOF);

raga_directive : ('Raga' '=') ALPHA (',' NUMBER)? (WS+|EOF); // Add a sanity check to keep this between 0 - 11
tala_directive : ('Tala' '=') ALPHA (',' NUMBER)? (WS+|EOF); // What would be a sane value for NUMBER ?
	
note	: (OCTAVE_SHIFTER)? NOTE (NUMBER)* (WS+ | EOF);

rest	: REST (NUMBER)* (WS+|EOF);

//TODO: remove dependency on jfugue
jfugue_passthru 
	:	('jfugue' ':') (ALPHA|NUMBER|'_'|'['|']')* (WS+|EOF);

OCTAVE_SHIFTER : ('<'+ | '>'+);

REST:('z' | 'Z');

NOTE :	( 's' | 'r' | 'g' | 'm' | 'p' | 'd' | 'n' | 'S' | 'R' | 'G' | 'M' | 'P' | 'D' | 'N' );

NUMBER  : (DIGIT)+ ;
 
WS : ( '\t' | ' ' | '\r' | '\n'| '\u000C' )+;
 
fragment DIGIT  : '0'..'9' ;

ALPHA 	:	('a'..'z'|'A'..'Z')+;

COMMENT :	'%' ( ~('\r'|'\n')* ) '\r'? '\n'+ {skip();};

PHRASE_SEPARATOR: '-';

